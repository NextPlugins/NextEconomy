plugins {
    id 'java'

    id 'me.bristermitten.pdm' version '0.0.30'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '1.2.0'

tasks.build.dependsOn('pdm', 'shadowJar')

repositories {
    mavenCentral()
    mavenLocal()

    // spigot

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // libs

    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {

    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"

    /*** local dependency (nChat) ***/
    compileOnly 'com.nickuc:chat:1.0.0'

    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'net.citizensnpcs:citizens:2.0.27-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'br.net.fabiozumbi12:ultimatechat:1.9.1'

    implementation 'de.tr7zw:item-nbt-api:2.7.1'
    implementation 'com.github.HenryFabio:inventory-api:2.0.3'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'
    implementation 'com.github.HenryFabio:configuration-injector:main-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:1.8'

    pdm 'com.github.ben-manes.caffeine:caffeine:2.9.0' // 3.0.0 or higher is for java 11+
    implementation 'com.github.SaiintBrisson:command-framework:1.2.0'

    def lombok = 'org.projectlombok:lombok:1.18.16'
    compileOnly lombok
    annotationProcessor lombok

}

bukkit {
    main = 'com.nextplugins.economy.NextEconomy'
    authors = ['NextPlugins']
    website = 'https://nextplugins.com.br'
    version = "${project.version}"
    depend = ['Vault']
    apiVersion = '1.16'
    load = 'STARTUP'
    softDepend = ['Citizens', 'HolographicDisplays', 'PlaceholderAPI', 'Legendchat']
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")
    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.economy.libs.inventoryapi'
    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.economy.libs.sqlprovider'
    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.economy.libs.configinjector'
    relocate 'de.tr7zw.changeme.nbtapi', 'com.nextplugins.economy.libs.nbtapi'
    relocate 'me.saiintbrisson', 'com.nextplugins.economy.libs.commandframework'
    relocate 'org.bstats', 'com.nextplugins.economy.libs.bstats'
}

compileJava {
    options.encoding('UTF-8')

    sourceCompatibility('1.8')
    targetCompatibility('1.8')
}
