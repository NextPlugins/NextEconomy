plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

group 'com.nextplugins'
version "2.1.0-${getGitHash}"

tasks.build.dependsOn('shadowJar')

repositories {
    mavenCentral()

    // spigot

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // libs

    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.citizensnpcs.co/' }
    maven { url = 'https://m2.dv8tion.net/releases' }
    maven { url = 'https://jitpack.io/' }
    maven { url = 'https://repo.nickuc.com/maven2/' }
    maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
    maven { url = 'https://raw.githubusercontent.com/FabioZumbi12/UltimateChat/mvn-repo/' }
}


dependencies {
    compileOnly "org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT"

    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'net.citizensnpcs:citizensapi:2.0.27-SNAPSHOT'
    // For some reason the + recent version stopped working in the repository.
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'com.nickuc.chat:nchat-api:4.2'
    compileOnly fileTree('libs')

    implementation 'com.github.Yuhtin:update-checker:21571485a3'
    implementation 'com.github.HenryFabio.configuration-injector:bukkit:1.0.2'
    implementation 'com.github.HenryFabio:inventory-api:main-SNAPSHOT'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'

    implementation 'com.github.SaiintBrisson.command-framework:bukkit:1.2.0'
    implementation 'com.github.tr7zw.Item-NBT-API:item-nbt-api:e0c060a982'

    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.0' // 3.0.0 or higher is for java 11+

    def lombok = 'org.projectlombok:lombok:1.18.22'
    compileOnly lombok
    annotationProcessor lombok
}

bukkit {
    main = 'com.nextplugins.economy.NextEconomy'
    authors = ['NextPlugins']
    website = 'https://nextplugins.com.br'
    version = "${project.version}"
    depend = ['Vault']
    apiVersion = '1.13'
    load = 'STARTUP'
    softDepend = [
            'Citizens',
            'NextTestServer',
            'CMI',
            'SkinsRestorer',
            'HolographicDisplays',
            'PlaceholderAPI',
            'Legendchat',
            'UltimateChat',
            'nChat',
            'DiscordSRV'
    ]
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")

    relocate 'com.yuhtin.updatechecker', 'com.nextplugins.economy.libs.updatechecker'

    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.economy.libs.configinjector'
    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.economy.libs.inventoryapi'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.economy.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.economy.libs.hikari'

    relocate 'com.github.benmanes.caffeine', 'com.nextplugins.economy.libs.caffeine'

    relocate 'me.saiintbrisson.minecraft.command', 'com.nextplugins.economy.libs.command.common'
    relocate 'me.saiintbrisson.bukkit.command', 'com.nextplugins.economy.libs.command.bukkit'

    relocate 'de.tr7zw.changeme.nbtapi', 'com.nextplugins.economy.libs.nbtapi'
    relocate 'de.tr7zw.annotations', 'com.nextplugins.economy.libs.annotations'

    relocate 'com.google.errorprone.annotations', 'com.nextplugins.economy.libs.annotations'

}

compileJava {
    options.encoding('UTF-8')

    sourceCompatibility('1.8')
    targetCompatibility('1.8')
}
