plugins {
    id 'java'

    id 'me.bristermitten.pdm' version '0.0.30'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group = 'com.nextplugins'
version = '1.1.0'

tasks.build.dependsOn('pdm', 'shadowJar')

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    // spigot

    maven { url = 'http://maven.elmakers.com/repository/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // libs

    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {

    compileOnly 'org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'

    /*** local dependency (legendchat) ***/
    compileOnly 'br.com.devpaulo:legendchat:1.1.5'

    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'net.citizensnpcs:citizens:2.0.27-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'

    implementation 'com.github.HenryFabio:inventory-api:2.0.3'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'
    implementation 'com.github.HenryFabio:configuration-injector:main-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:1.8'

    pdm 'com.github.NextPlugins:data-converter:experimental-0.0.1'
    pdm 'com.github.SaiintBrisson:command-framework:1.1.0'

    def lombok = 'org.projectlombok:lombok:1.18.16'
    compileOnly lombok
    annotationProcessor lombok

}

bukkit {
    main = 'com.nextplugins.economy.NextEconomy'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    depend = ['Vault']
    load = 'STARTUP'
    softDepend = ['Citizens', 'HolographicDisplays', 'PlaceholderAPI', 'Legendchat']
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")
    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.economy.libs.inventoryapi'
    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.economy.libs.sqlprovider'
    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.economy.libs.configinjector'
    relocate 'org.bstats', 'com.nextplugins.economy.libs.bstats'
}

compileJava {
    options.encoding('UTF-8')

    sourceCompatibility('1.8')
    targetCompatibility('1.8')
}
