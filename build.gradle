plugins {
    id 'java'

    id 'me.bristermitten.pdm' version '0.0.30'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '2.0.0-TEST-VERSION'

tasks.build.dependsOn('pdm', 'shadowJar')

repositories {
    mavenCentral()
    mavenLocal()

    // spigot

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // libs

    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.citizensnpcs.co/' }
    maven { url = 'https://m2.dv8tion.net/releases' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {

    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"

    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'net.citizensnpcs:citizens:2.0.27-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'br.net.fabiozumbi12:ultimatechat:1.9.1'
    compileOnly 'com.nickuc:chat:1.0.0'

    implementation 'com.github.HenryFabio.configuration-injector:common:main-SNAPSHOT'
    implementation 'com.github.HenryFabio.configuration-injector:bukkit:main-SNAPSHOT'

    pdm 'de.tr7zw:item-nbt-api:2.7.1'
    pdm 'org.bstats:bstats-bukkit:1.8'

    pdm 'com.github.HenryFabio:inventory-api:2.0.3'
    pdm 'com.github.HenryFabio:sql-provider:9561f20fd2'

    pdm 'com.github.ben-manes.caffeine:caffeine:2.9.0' // 3.0.0 or higher is for java 11+

    pdm ('net.dv8tion:JDA:4.2.1_259') {
        exclude module: 'opus-java'
    }

    pdm 'com.github.SaiintBrisson.command-framework:bukkit:1.2.0'
    pdm 'com.github.SaiintBrisson.command-framework:shared:1.2.0'

    def lombok = 'org.projectlombok:lombok:1.18.16'
    compileOnly lombok
    annotationProcessor lombok

}

bukkit {
    main = 'com.nextplugins.economy.NextEconomy'
    authors = ['NextPlugins']
    website = 'https://nextplugins.com.br'
    version = "${project.version}"
    depend = ['Vault']
    apiVersion = '1.16'
    load = 'STARTUP'
    softDepend = ['Citizens', 'HolographicDisplays', 'PlaceholderAPI', 'Legendchat', 'nChat']
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")
    relocate('com.henryfabio.minecraft.configinjector', 'com.nextplugins.economy.libs.configinjector')
}

compileJava {
    options.encoding('UTF-8')

    sourceCompatibility('1.8')
    targetCompatibility('1.8')
}
